package fr.utbm.ia54.Agent

import io.sarl.core.Initialize
import io.sarl.core.Logging
import fr.utbm.ia54.gui.fx.EndSimulation
import io.sarl.core.InnerContextAccess
import io.sarl.core.Lifecycle
import fr.utbm.ia54.Event.ConfigureSimulation
import fr.utbm.ia54.Event.CreateMap
import fr.utbm.ia54.gui.TaquinFxViewerController
import io.sarl.core.Behaviors

agent BootAgent {
	uses Logging, Behaviors, InnerContextAccess, Lifecycle
	
	var probSize : int
	var exited : boolean = false

	var ctrl : TaquinFxViewerController

	// Fired when pressed BUTTON on UI
	on Initialize {
		loggingName = "BootAgent-" + this.ID
		ctrl = occurrence.parameters.get(0) as TaquinFxViewerController
		ctrl.UISpace.register(asEventListener)
		info("Configuration of the environment...")

	}
	
	on ConfigureSimulation {
		this.probSize = occurrence.probSize
		info('PROB SIZE quantity : ' + this.probSize)
		
		// START ECO RESOLUTION
		spawn(BoardGameAgent, #[this.probSize, ctrl])
	}

	on EndSimulation [!hasMemberAgent] {
		info("The simulation has ended.")
		killMe
	}
	
	on EndSimulation [hasMemberAgent && !exited] {
		exited = true
		innerContext.defaultSpace.emit(ID, new EndSimulation)
	}
}
